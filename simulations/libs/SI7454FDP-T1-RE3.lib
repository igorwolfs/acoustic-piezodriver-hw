*Feb 13, 2006
*Doc. ID: 77252, S-60145, Rev. B
*File Name: Si7454DP_PS.txt and Si7454DP_PS.lib
*This document is intended as a SPICE modeling guideline and does not
*constitute a commercial product data sheet.  Designers should refer to the
*appropriate data sheet of the same number for guaranteed specification
*limits.
.SUBCKT Si7454DP 4 1 2
M1  3 1 2 2 NMOS W=3036751u L=0.50u 
M2  2 1 2 4 PMOS W=3036751u L=0.35u
R1  4 3     RTEMP 21E-3
CGS 1 2     940E-12
DBD 2 4     DBD
***************************************************************** 
.MODEL  NMOS       NMOS  ( LEVEL  = 3            TOX    = 7E-8
+ RS     = 6E-3            RD     = 0            NSUB   = 2.2E17   
+ kp     = 4.2E-5          UO     = 650             
+ VMAX   = 0               XJ     = 5E-7         KAPPA  = 150E-2
+ ETA    = 1E-4            TPG    = 1  
+ IS     = 0               LD     = 0                             
+ CGSO   = 0               CGDO   = 0            CGBO   = 0 
+ NFS    = 0.8E12          DELTA  = 0.1)
***************************************************************** 
.MODEL  PMOS       PMOS ( LEVEL   = 3            TOX    = 7E-8
+NSUB    = 1.15E16        TPG     = -1)   
***************************************************************** 
.MODEL DBD D (CJO=830e-12 VJ=0.38 M=0.4 
+FC=0.1 IS=1E-12 TT=3E-8 N=1 BV=100.5)
***************************************************************** 
.MODEL RTEMP RES (TC1=7.5E-3 TC2=5.5E-6)
***************************************************************** 
.ENDS